cmake_minimum_required(VERSION 3.18.0)

enable_testing()

project(
	Clove 
	VERSION 0.5.0
	LANGUAGES C CXX
)

set(CMAKE_CXX_STANDARD 20)
set(CMAKE_CXX_STANDARD_REQUIRED ON)

set(CMAKE_DOTNET_TARGET_FRAMEWORK_VERSION "v4.7.2")

set(CMAKE_ARCHIVE_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/Clove)
set(CMAKE_LIBRARY_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/Clove)
set(CMAKE_RUNTIME_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/Clove)

#Project wide settings
option(CLOVE_BUILD_BULB "Build Bulb, Clove's editor" ON)
option(CLOVE_BUILD_TESTS "Build Clove's tests" OFF)
option(CLOVE_MANUALLY_INSTALL_PACKAGES "Stop CMake automatically installing conan packages when configuring" OFF)

#Required for linking shared object (*.so) files on Linux
set(CMAKE_POSITION_INDEPENDENT_CODE ON)

#Set up conan
if(NOT CLOVE_MANUALLY_INSTALL_PACKAGES)
	if(NOT EXISTS "${CMAKE_BINARY_DIR}/conan.cmake")
   		message(STATUS "Downloading conan.cmake from https://github.com/conan-io/cmake-conan")
   		file(DOWNLOAD "https://raw.githubusercontent.com/conan-io/cmake-conan/master/conan.cmake"
        	          "${CMAKE_BINARY_DIR}/conan.cmake")
	endif()
	include(${CMAKE_BINARY_DIR}/conan.cmake)

	if(${GENERATOR_IS_MULTI_CONFIG})
		conan_cmake_run(CONANFILE conanfile.txt BASIC_SETUP CMAKE_TARGETS BUILD missing CONFIGURATION_TYPES "${CMAKE_CONFIGURATION_TYPES}")
	else()
		conan_cmake_run(CONANFILE conanfile.txt BASIC_SETUP CMAKE_TARGETS BUILD missing BUILD_TYPE "${CMAKE_BUILD_TYPE}")
	endif()	
else()
	include(${CMAKE_BINARY_DIR}/conanbuildinfo.cmake)
	conan_basic_setup(TARGETS)
endif()

#Engine
add_subdirectory(clove)

#Editor
if(CLOVE_BUILD_BULB AND MSVC)
	add_subdirectory(bulb)
endif()